group = "com.adguard.biometric_cipher"
version = "1.0-SNAPSHOT"

buildscript {
    ext.kotlin_version = "1.9.25"
    ext.mockito_version = '5.16.0'
    ext.mockito_kotlin_version = "5.4.0"
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.5.2")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}
// -----------------------------
// Mockito Agent Configuration
// -----------------------------
configurations {
    mockitoAgent
}

dependencies {
    // Adding Mockito as a Java Agent
    mockitoAgent("org.mockito:mockito-core:$mockito_version") {
        transitive = false
    }
}

tasks.withType(Test).configureEach {
    // Adding the Mockito Java Agent to the JVM arguments
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    if (project.android.hasProperty("namespace")) {
        namespace = "com.adguard.biometric_cipher"
    }

    compileSdk = 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    defaultConfig {
        minSdk = 23
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    dependencies {
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3")
        implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3")
        implementation("androidx.security:security-crypto-ktx:1.1.0-alpha06")
        implementation("androidx.biometric:biometric-ktx:1.2.0-alpha05")

        // Unit Testing Dependencies
        testImplementation("junit:junit:4.13.2")
        testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3")
        testImplementation("org.mockito:mockito-core:$mockito_version")
        testImplementation("org.mockito.kotlin:mockito-kotlin:$mockito_kotlin_version")
        testImplementation("org.robolectric:robolectric:4.12.1")

        // Android Instrumented Testing Dependencies
        androidTestImplementation("androidx.test.ext:junit-ktx:1.2.1")
        androidTestImplementation("androidx.test:runner:1.6.2")
        androidTestImplementation("androidx.test:rules:1.6.1")
        androidTestImplementation("androidx.test.espresso:espresso-core:3.6.1")
        androidTestImplementation("org.mockito:mockito-android:5.5.0")
        androidTestImplementation("org.mockito.kotlin:mockito-kotlin:$mockito_kotlin_version")
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
    }
}
